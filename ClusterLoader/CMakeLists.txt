cmake_minimum_required(VERSION 3.17)

set(CLUSTERLOADER "ClusterLoader")

PROJECT(${CLUSTERLOADER})

set(CMAKE_AUTOMOC ON)

if(MSVC)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /W3 /DWIN32 /EHsc /MP /permissive- /Zc:__cplusplus")
    set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} /NODEFAULTLIB:LIBCMT")
    set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} /MDd")
    set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} /MD")
endif(MSVC)

find_package(Qt6 COMPONENTS Widgets WebEngineWidgets REQUIRED)

set(CLUSTERLOADER_SOURCES
    src/ClusterLoader.h
    src/ClusterLoader.cpp
    src/ClusterLoader.json
)

set(PLUGIN_MOC_HEADERS
    src/ClusterLoader.h
)

source_group(Plugin FILES ${CLUSTERLOADER_SOURCES})

add_library(${CLUSTERLOADER} SHARED ${CLUSTERLOADER_SOURCES})

qt_wrap_cpp(CLUSTERLOADER_MOC ${PLUGIN_MOC_HEADERS} TARGET ${CLUSTERLOADER})
target_sources(${CLUSTERLOADER} PRIVATE ${CLUSTERLOADER_MOC})

target_include_directories(${CLUSTERLOADER} PRIVATE "${INSTALL_DIR}/$<CONFIGURATION>/include/")

target_compile_features(${CLUSTERLOADER} PRIVATE cxx_std_17)

set(HDPS_LINK_PATH "${INSTALL_DIR}/$<CONFIGURATION>/lib")
set(PLUGIN_LINK_PATH "${INSTALL_DIR}/$<CONFIGURATION>/$<IF:$<CXX_COMPILER_ID:MSVC>,lib,Plugins>")
set(HDPS_LINK_SUFFIX $<IF:$<CXX_COMPILER_ID:MSVC>,${CMAKE_LINK_LIBRARY_SUFFIX},${CMAKE_SHARED_LIBRARY_SUFFIX}>)

set(HDPS_LINK_LIBRARY "${HDPS_LINK_PATH}/${CMAKE_SHARED_LIBRARY_PREFIX}HDPS_Public${HDPS_LINK_SUFFIX}")
set(POINTDATA_LINK_LIBRARY "${PLUGIN_LINK_PATH}/${CMAKE_SHARED_LIBRARY_PREFIX}PointData${HDPS_LINK_SUFFIX}") 
set(CLUSTERDATA_LINK_LIBRARY "${PLUGIN_LINK_PATH}/${CMAKE_SHARED_LIBRARY_PREFIX}ClusterData${HDPS_LINK_SUFFIX}") 

target_link_libraries(${CLUSTERLOADER} PRIVATE Qt6::Widgets)
target_link_libraries(${CLUSTERLOADER} PRIVATE Qt6::WebEngineWidgets)
target_link_libraries(${CLUSTERLOADER} PRIVATE "${HDPS_LINK_LIBRARY}")
target_link_libraries(${CLUSTERLOADER} PRIVATE "${POINTDATA_LINK_LIBRARY}")
target_link_libraries(${CLUSTERLOADER} PRIVATE "${CLUSTERDATA_LINK_LIBRARY}")

install(TARGETS ${CLUSTERLOADER}
    RUNTIME DESTINATION Plugins COMPONENT PLUGINS # Windows .dll
    LIBRARY DESTINATION Plugins COMPONENT PLUGINS # Linux/Mac .so
)

add_custom_command(TARGET ${CLUSTERLOADER} POST_BUILD
    DEPENDS ClusterLoader
    COMMAND "${CMAKE_COMMAND}"
        --install ${CMAKE_CURRENT_BINARY_DIR}
        --config $<CONFIGURATION>
        --prefix ${INSTALL_DIR}/$<CONFIGURATION>
)

# Automatically set the debug environment (command + working directory) for MSVC
if(MSVC)
    set_property(TARGET ${CLUSTERLOADER} PROPERTY VS_DEBUGGER_WORKING_DIRECTORY  $<IF:$<CONFIG:DEBUG>,${INSTALL_DIR}/debug,${INSTALL_DIR}/release>)
    set_property(TARGET ${CLUSTERLOADER} PROPERTY VS_DEBUGGER_COMMAND $<IF:$<CONFIG:DEBUG>,${INSTALL_DIR}/debug/HDPS.exe,${INSTALL_DIR}/release/HDPS.exe>)
endif()
